plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.android.extensions'
    id 'org.jetbrains.kotlin.kapt'
}

def gitCommitCount = "git rev-list HEAD --count".execute().text.trim()

android {
    compileSdkVersion 29
    signingConfigs {
        release
        github {
            storeFile file("../yun.jks")
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    defaultConfig {
        applicationId "me.liuyun.bjutlgn"
        minSdkVersion 28
        targetSdkVersion 29
        versionCode gitCommitCount.toInteger()
        versionName "11.0.0"
        versionNameSuffix = " (${gitCommitCount})"
        resConfigs "en", "zh-rCN"
        manifestPlaceholders = [app_name: "@string/app_name"]
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [app_name: "@string/app_name_debug"]
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        releaseGitHub {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.github
        }
    }
    packagingOptions {
        exclude 'kotlin/**'
        exclude 'META-INF/*.kotlin_module'
        exclude 'META-INF/*.version'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.2"
    implementation "org.jetbrains.anko:anko-commons:0.10.8"

    implementation 'com.google.android.material:material:1.2.0-alpha02'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.core:core-ktx:1.2.0-rc01'
    implementation 'androidx.dynamicanimation:dynamicanimation:1.1.0-alpha02'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0-rc02'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-rc02'
    implementation "androidx.preference:preference:1.1.0"
    implementation 'androidx.room:room-runtime:2.2.2'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0-alpha03'
    kapt 'androidx.room:room-compiler:2.2.2'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.14'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.squareup.okhttp3:okhttp:4.2.2'
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation 'com.squareup.retrofit2:converter-scalars:2.6.2'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.2'
    implementation 'me.itangqi.waveloadingview:library:0.3.5'
    implementation 'com.artit-k:license-fragment:1.3.0'
}

def propFile = file('../signing.properties')
if (propFile.canRead()) {
    Properties p = new Properties()
    p.load(new FileInputStream(propFile))

    if (p != null
            && p.containsKey('STORE_FILE')
            && p.containsKey('STORE_PASSWORD')
            && p.containsKey('KEY_ALIAS')
            && p.containsKey('KEY_PASSWORD')
    ) {
        println "Signing..."

        android.signingConfigs.release.storeFile = file(p['STORE_FILE'])
        android.signingConfigs.release.storePassword = p['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = p['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = p['KEY_PASSWORD']

    } else {
        println "Required properties in signing.properties are missing"
        android.buildTypes.release.signingConfig = null
    }

} else {
    println "signing.properties not found"
    android.buildTypes.release.signingConfig = null
}
