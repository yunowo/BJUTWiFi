plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.android.extensions'
    id 'org.jetbrains.kotlin.kapt'
    id 'com.github.triplet.play'
}

def gitCommitCount = "git rev-list HEAD --count".execute().text.trim()

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    signingConfigs {
        release
        github {
            storeFile file("../yun.jks")
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    playAccountConfigs {
        defaultAccountConfig {
            jsonFile = file('../play.json')
        }
    }
    defaultConfig {
        applicationId "me.liuyun.bjutlgn"
        minSdkVersion 25
        targetSdkVersion 27
        versionCode gitCommitCount.toInteger()
        versionName "10.0.0"
        versionNameSuffix = " (${gitCommitCount})"
        resConfigs "en", "zh-rCN"
        manifestPlaceholders = [app_name: "@string/app_name"]
        playAccountConfig = playAccountConfigs.defaultAccountConfig
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [app_name: "@string/app_name_debug"]
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        releaseGitHub {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.github
            play {
                track = 'beta'
                userFraction = 1.0
                untrackOld = true
            }
        }
    }
    packagingOptions {
        exclude 'kotlin/**'
        exclude 'META-INF/*.kotlin_module'
        exclude 'META-INF/*.version'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}
dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.22.1"
    implementation "org.jetbrains.anko:anko-commons:$anko_version"

    implementation "com.android.support:design:$support_lib_version"
    implementation "com.android.support:cardview-v7:$support_lib_version"
    implementation "com.android.support:support-dynamic-animation:$support_lib_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta5'
    implementation "android.arch.lifecycle:common-java8:$lifecycle_version"
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    implementation "android.arch.persistence.room:runtime:$room_version"
    kapt "android.arch.persistence.room:compiler:$room_version"
    implementation 'androidx.core:core-ktx:0.2'

    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.squareup.okhttp3:okhttp:3.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'me.itangqi.waveloadingview:library:0.3.5'
    implementation 'com.artit-k:license-fragment:1.3.0'
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

def propFile = file('../signing.properties')
if (propFile.canRead()) {
    Properties p = new Properties()
    p.load(new FileInputStream(propFile))

    if (p != null
            && p.containsKey('STORE_FILE')
            && p.containsKey('STORE_PASSWORD')
            && p.containsKey('KEY_ALIAS')
            && p.containsKey('KEY_PASSWORD')
    ) {
        println "Signing..."

        android.signingConfigs.release.storeFile = file(p['STORE_FILE'])
        android.signingConfigs.release.storePassword = p['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = p['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = p['KEY_PASSWORD']

    } else {
        println "Required properties in signing.properties are missing"
        android.buildTypes.release.signingConfig = null
    }

} else {
    println "signing.properties not found"
    android.buildTypes.release.signingConfig = null
}
