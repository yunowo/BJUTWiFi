apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.trevjonez.AndroidGithubReleasePlugin'

def gitCommitCount = "git rev-list HEAD --count".execute().text.trim()

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    signingConfigs {
        release
    }
    defaultConfig {
        applicationId "me.liuyun.bjutlgn"
        minSdkVersion 23
        targetSdkVersion 25
        versionCode 4
        versionName "0.4"
        resConfigs "en", "zh-rCN"
    }
    buildTypes {
        debug {
            versionNameSuffix = " (${gitCommitCount})"
            signingConfig signingConfigs.release
        }
        release {
            versionNameSuffix = " (${gitCommitCount})"
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:support-dynamic-animation:25.3.1'
    compile 'io.reactivex.rxjava2:rxjava:2.0.6'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-scalars:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    compile 'com.jakewharton:butterknife:8.5.1'
    compile 'com.github.HotBitmapGG:RingProgressBar:V1.2.2'
    compile 'com.j256.ormlite:ormlite-android:5.0'
    compile 'net.sourceforge.streamsupport:streamsupport:1.5.3'
    compile 'com.artit-k:license-fragment-support-v4:1.3.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
}

AndroidGithubRelease {
    defaultConfig {
        tagName android.defaultConfig.versionName + "-${gitCommitCount}"
        owner "liu-yun"
        repo "BJUTWiFi"
        assets "build/outputs/apk"
        overwrite true
    }

    androidConfigs {
        debug {
            preRelease true
            tagModifier { it + "-debug" }
        }
        release {
            draft true
            releaseName android.defaultConfig.versionName
        }
    }
}

def propFile = file('../signing.properties')
if (propFile.canRead()) {
    Properties p = new Properties()
    p.load(new FileInputStream(propFile))

    if (p != null
            && p.containsKey('STORE_FILE')
            && p.containsKey('STORE_PASSWORD')
            && p.containsKey('KEY_ALIAS')
            && p.containsKey('KEY_PASSWORD')
            && p.containsKey('GITHUB_API_TOKEN')
    ) {
        println "Signing..."

        android.signingConfigs.release.storeFile = file(p['STORE_FILE'])
        android.signingConfigs.release.storePassword = p['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = p['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = p['KEY_PASSWORD']
        AndroidGithubRelease.defaultConfig.accessToken = p['GITHUB_API_TOKEN']

    } else {
        println "Required properties in signing.properties are missing"
        android.buildTypes.release.signingConfig = null
    }

} else {
    println "signing.properties not found"
    android.buildTypes.release.signingConfig = null
}